apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

repositories {
    mavenCentral()
    maven {
        url "https://raw.github.com/platypusllc/core/mvn-repo/"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    playAccountConfigs {
        release {
            jsonFile = file(System.getenv("ANDROID_PLAY_JSON_FILE") ?: ".")
        }
    }
    signingConfigs {
        release {
            keyAlias System.getenv("ANDROID_RELEASE_KEY_ALIAS") ?: "EXAMPLE_ALIAS"
            keyPassword System.getenv("ANDROID_RELEASE_KEY_PASSWORD") ?: "EXAMPLE_PASSWORD"
            storeFile file(System.getenv("ANDROID_RELEASE_STORE_FILE") ?: ".")
            storePassword System.getenv("ANDROID_RELEASE_STORE_PASSWORD") ?: "EXAMPLE_PASSWORD"
            v2SigningEnabled true
        }
    }
    defaultConfig {
        applicationId "com.platypus.android.server"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 3
        versionName "1.3"
        playAccountConfig = playAccountConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }
    dexOptions {
        javaMaxHeapSize '4g'
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        // See: https://guides.codepath.com/android/Setting-up-Travis-CI
        preDexLibraries = preDexEnabled && !travisBuild
    }
}

play {
    track = 'alpha'
    untrackOld = true
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:support-v13:24.0.0'
    compile 'com.android.support:support-annotations:24.0.0'
    compile 'com.google.firebase:firebase-database:9.2.0'
    compile 'com.google.firebase:firebase-storage:9.2.0'
    compile 'com.google.firebase:firebase-auth:9.2.0'
    compile 'com.google.firebase:firebase-crash:9.2.0'
    compile 'com.journeyapps:zxing-android-embedded:3.3.0'
    compile ('org.jscience:jscience:4.3.+') {
        exclude group: 'org.javolution', module: 'javolution'
    }
    compile group: 'com.platypus.crw', name: 'platypus-core', version: '+', changing: true
}

// Google Services are required to support Firebase.
apply plugin: 'com.google.gms.google-services'
